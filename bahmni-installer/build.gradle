plugins {
    id "nebula.os-package" version "2.2.6"
}

group 'org.bahmni.installer'
version project.bahmniRelease

apply plugin: 'rpm'

task extractBahmniPlaybooks(type: Copy) {
    from zipTree(file("${projectDir}/resources/bahmni-playbooks.zip"))
    into file("${projectDir}/resources")
}

buildRpm {
    dependsOn 'extractBahmniPlaybooks'
    packageName 'bahmni-installer'
    release System.getenv('GO_PIPELINE_COUNTER')?: "1"
    arch NOARCH
    os LINUX
    user 'bahmni'
    permissionGroup 'bahmni'

    into '/opt/bahmni-installer'

    requires('epel-release')

    preInstall file("${projectDir}/scripts/preinstall.sh")
    postInstall file("${projectDir}/scripts/postinstall.sh")

    from ("${projectDir}/resources/") {
        fileMode = 0755
        createDirectoryEntry = true
        include('bahmni-playbooks/')
    }

    from ("${projectDir}/resources/rpm_versions.yml") {
        fileMode = 0644
        createDirectoryEntry = true
        into 'etc'
    }

    from ("${projectDir}/resources/ansible.cfg") {
        fileMode = 0644
        createDirectoryEntry = true
        into 'bahmni-playbooks'
    }

    from("${projectDir}/scripts/rpm/") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'bin'
    }
}
