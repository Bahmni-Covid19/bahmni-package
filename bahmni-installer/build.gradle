plugins {
    id "nebula.ospackage" version "3.4.0"
}

group 'org.bahmni.installer'
version project.bahmniRelease

task extractBahmniPlaybooks(type: Copy) {
    from zipTree(file("${projectDir}/resources/bahmni-playbooks.zip"))
    into file("${projectDir}/resources")
}

ospackage {
    packageName = 'bahmni-installer'
    String initialCounter = System.getenv('INITIAL_PIPELINE_COUNTER') ?: "0"
    String currentCounter=System.getenv('GO_PIPELINE_COUNTER') ?: "1"
    int currentBuildNumber = currentCounter as Integer
    int initialBuildNumber=0;
    if (initialCounter.isInteger()) {
        initialBuildNumber = initialCounter as Integer
    }
    int newBuildNumber = initialBuildNumber + currentBuildNumber
    release = newBuildNumber as String
    arch = NOARCH
    os = LINUX
    user = 'bahmni'
    permissionGroup = 'bahmni'

    into '/opt/bahmni-installer'

    from ("${projectDir}/resources/") {
        fileMode = 0755
        createDirectoryEntry = true
        include('bahmni-playbooks/')
        include('bahmni-command-line-tool/')
    }

    from ("${projectDir}/resources/rpm_versions.yml") {
        fileMode = 0644
        createDirectoryEntry = true
        into 'etc'
    }

    from ("${projectDir}/resources/ansible.cfg") {
        fileMode = 0644
        createDirectoryEntry = true
        into 'bahmni-playbooks'
    }

    from("${projectDir}/scripts/rpm/") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'bin'
    }
}

buildRpm {
    dependsOn 'extractBahmniPlaybooks'

    requires('python-devel')
    requires('python-setuptools')
    requires('epel-release')

    preInstall file("${projectDir}/scripts/preinstall.sh")
    postInstall file("${projectDir}/scripts/postinstall.sh")
}
