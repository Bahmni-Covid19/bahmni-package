plugins {
    id "nebula.os-package" version "2.2.6"
}

group 'org.bahmni.web'
version project.bahmniRelease

apply plugin: 'rpm'

task extractBahmniApps(type: Copy){
        from zipTree(file("${projectDir}/resources/bahmniapps.zip"))
    into file("${projectDir}/resources/bahmniapps")
}

task extractBahmniConfig(type: Copy){
    from zipTree(file("${projectDir}/resources/default_config.zip"))
    into file("${projectDir}/resources/bahmni_config")
}


task dist(dependsOn: ["extractBahmniApps", "extractBahmniConfig"], type: Rpm) {
    packageName 'bahmni-web'
    release System.getenv('GO_PIPELINE_COUNTER') ?: project.buildNumber
    arch NOARCH
    os LINUX
    user 'root'

    into '/opt/bahmni-web'

    requires('bahmni-certs')
    requires('httpd')
    requires('mod_ssl')
    requires('mod_wsgi')
    requires('python-flask') //This is required for client_side_logging

    postInstall file("${projectDir}/scripts/postinstall.sh")
    postUninstall file("${projectDir}/scripts/postuninstall.sh")


    from("${projectDir}/resources/") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'etc'
        exclude('bahmniapps.zip')
        exclude('default_config.zip')
    }

}
